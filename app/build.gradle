apply plugin: 'com.android.application'

task gitinfo {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            executable = 'git'
            args = ['describe','--dirty']
            standardOutput = os
        }
        ext.describe = os.toString().trim()
        def split = ext.describe.split("-")
        ext.version = Integer.parseInt(split[0].substring(1)) * 1000 + Integer.parseInt(split[1])
        ext.mediaver = 'git -C ../media describe --dirty'.execute().text.trim()
        println "Version Name: " + ext.describe + ", Version Code: " + ext.version
    }
    println 'This version of leanfront should use media branch 1.2.0-lf'
    println 'Media Version: ' + ext.mediaver
}

android {
    lintOptions{
        disable 'MissingTranslation'
    }
    compileSdk 34
    defaultConfig {
        applicationId "org.mythtv.leanfront"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode gitinfo.ext.version
        versionName gitinfo.ext.describe
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.debug
            debuggable true
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        google {
            initWith release
            debuggable false
        }
        debug {
            minifyEnabled false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    externalNativeBuild {
//        cmake {
//            path file('src/main/jni/CMakeLists.txt')
//        }
//    }
    ndkVersion '21.3.6528147'
    namespace 'org.mythtv.leanfront'

    dexOptions {
        javaMaxHeapSize = "4G"
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.leanback:leanback:1.1.0-rc02'
    implementation 'androidx.recommendation:recommendation:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.1.0-rc01'
    implementation 'androidx.annotation:annotation:1.6.0'

    implementation 'com.github.bumptech.glide:glide:4.9.0'

//    implementation 'com.google.android.exoplayer:exoplayer:2.18.0'
//    implementation 'com.google.android.exoplayer:extension-leanback:2.18.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

//    implementation "androidx.media3:media3-exoplayer:1.0.0"
//    implementation "androidx.media3:media3-ui-leanback:1.0.0"
//    implementation "androidx.media3:media3-ui:1.0.0"

    implementation project(':media-lib-exoplayer')
    implementation project(':media-lib-ui-leanback')
    implementation project(':media-lib-ui')
    implementation project(':media-lib-decoder-ffmpeg')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'

    implementation "android.arch.lifecycle:extensions:1.1.1"
}

apply plugin: 'idea'
idea {
    module {
        excludeDirs.add(file('app/src/main/jni/ffmpeg'))
    }
}
