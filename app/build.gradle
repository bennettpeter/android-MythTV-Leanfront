apply plugin: 'com.android.application'

task gitinfo {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            executable = 'git'
            args = ['describe','--dirty']
            standardOutput = os
        }
        ext.describe = os.toString().trim()
        def split = ext.describe.split("-")
        ext.version = Integer.parseInt(split[0].substring(1)) * 1000 + Integer.parseInt(split[1])
        println "Version Name: " + ext.describe + ", Version Code: " + ext.version
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "org.mythtv.leanfront"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode gitinfo.ext.version
        versionName gitinfo.ext.describe
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            minifyEnabled false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }
    ndkVersion '21.3.6528147'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.leanback:leanback:1.1.0-rc01'
    implementation 'androidx.recommendation:recommendation:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.1.0-rc01'
    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'com.github.bumptech.glide:glide:4.9.0'

    implementation 'com.google.android.exoplayer:exoplayer:2.14.2'
    implementation 'com.google.android.exoplayer:extension-leanback:2.14.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'

    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
}

apply plugin: 'idea'
idea {
    module {
        excludeDirs.add(file('app/src/main/jni/ffmpeg'))
    }
}
